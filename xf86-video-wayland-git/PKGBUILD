# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>
# Maintainer: Andrew Cook <ariscop@gmail.com>

pkgname=xf86-video-wayland-git
pkgver=8c70ac6
pkgrel=1
pkgdesc="X.org generic driver for XWayland"
arch=('i686' 'x86_64')
url="http://wayland.freedesktop.org/"
license=('MIT')
depends=('weston' 'wayland' 'xwayland-git')
makedepends=('randrproto' 'inputproto' 'dri2proto' 'xf86dgaproto' 'libxxf86dga')
replaces=(xf86-video-wlshm)
source=(git://anongit.freedesktop.org/xorg/driver/xf86-video-wayland xorg.conf)
md5sums=('SKIP' 'c7e427fb07781045af81349372f332ab')

pkgver() {
  cd xf86-video-wayland
  git describe --always | sed 's|-|.|g' 
}

build() {
  cd xf86-video-wayland
  export ACLOCAL_PATH=/opt/xwayland/share/aclocal/
  ./autogen.sh --prefix=/opt/xwayland/ \
        PKG_CONFIG_PATH=/opt/xwayland/lib/pkgconfig/
  make
}

package() {
  cd xf86-video-wayland
  make DESTDIR="${pkgdir}" install
  install -d 755 "${pkgdir}"/opt/xwayland/etc/X11/
  install -m 644 "${srcdir}"/xorg.conf "${pkgdir}"/opt/xwayland/etc/X11/xorg.conf
}
